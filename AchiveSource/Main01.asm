//=====================================================================================================================
// Metamorphosis
// By Prince / Phaze101
// 
// Using Kick Assembler
//=====================================================================================================================

// Info:
// Screen           - $0400 (1024)
// Charset          - $2000 (8192)
// Color Attributes - $2800 (10240)
// TilesSet         - $2900 (10496)
// Map Date         - $3400 (13312)

//=====================================================================================================================
// Variables in Page Zero
//=====================================================================================================================

* = $02 "zero Page Constants" virtual

.zp {
	MultiplyNum1:
		.byte $00			// Multiplication Num1

	MultiplyNum2:
		.byte $00			// Multiplication Num2

	MultiplyNum1Hi:				
		.byte $00			// Multiplication Result in MultiplyNum1 (Lo), MultiplyNum1 (Hi) 

	MapLoaderTileLookup:
		.word $0000			// Contains address of our Tile where it is

	MapLoaderMapLookup:			// This is where our Map Tiles begin
		.word $0000

	MapLoaderColumn:			// The Column we are in from the Map 
		.byte $00

	MapLoaderRow:				// The Row we are in from the Map 
		.byte $00
}

//=====================================================================================================================
// Constants
//=====================================================================================================================

.label SCREEN_RAM 	= 	$0400	// Base Address Screen

//=====================================================================================================================
// BASIC startup - SYS to Entry
//=====================================================================================================================

BasicUpstart2(Entry)

//=====================================================================================================================
// Import maploader.asm
//=====================================================================================================================

#import "maploader.asm"

//=====================================================================================================================
// The main entry routine
//=====================================================================================================================

Entry: 
		//=============================================================================================================
		// Turn Interrupts off
		//=============================================================================================================
		sei
		lda #$7f
		sta $dc0d		// 56333
		sta $dd0d		// 56589

		//=============================================================================================================
		// Switch Basic and Kernal off
		//=============================================================================================================
		lda #%00110101	// 35 - Bit 0-2 - RAM visible at $A000-$BFFF and $E000-$FFFF, I/O area visible at $D000-$DFFF
		sta $01
		cli

		//=============================================================================================================
		// Set the Black Border
		//=============================================================================================================
		lda #$00
		sta $d020

		//=============================================================================================================
		// Set Background, Multi 1, Multi 2
		//=============================================================================================================
		lda #$00
		sta $d021
		lda #$0b		
		sta $d022
		lda #$09		
		sta $d023

		//=============================================================================================================
		// Same Screen Ram $0400, Char Mem $2000
		//=============================================================================================================
		lda #%00011000
		sta $d018

		//=============================================================================================================
		// Setting Multi Colour Mode On
		//=============================================================================================================
		lda $d016
		ora #%00010000
		sta $d016

		jsr ClearScreen

		//=============================================================================================================
		// Pass X and Y starting position of Screen
		// Screen is part of multiple screens found in the Map
		// It also means that we can draw a screen starting at any position in the map
		//=============================================================================================================
		ldx #20
		ldy #99
		jsr MAPLOADER.DrawMap

	!Loop:
		jmp !Loop-


//=====================================================================================================================
// Clear Screen - Clears 1024 so it clears Sprites
//=====================================================================================================================

ClearScreen: {
		lda #$00
		ldx #250
	!:
		sta SCREEN_RAM-1, x
		sta SCREEN_RAM + 250-1, x
		sta SCREEN_RAM + 500-1, x
		sta SCREEN_RAM + 750-1, x
		dex
		bne !-
		rts
}

//=====================================================================================================================
// Data Sections
// Only the Character Set needs to be in Chipmem
//=====================================================================================================================

* = $2000 "Character Set"
	.import binary "maps/Metamorphosis/chars.bin"		// Chars Redefined 

* = $2800 "Colour Attributes"
	.import binary "maps/Metamorphosis/cols.bin" 		// Colour Data

* = $2900 "TileSet Data"
	.import binary "maps/Metamorphosis/tiles.bin" 		// Tile Data

* = $3400 "Map Data"
MapData:
	.import binary "maps/Metamorphosis/map.bin" 		// Map Data